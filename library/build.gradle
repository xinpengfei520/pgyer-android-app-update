apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion versions.compileSdkVersion
    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.libReleaseCode
        versionName versions.libReleaseName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publish {
    repoName = 'PgyerAndroidAppUpdate' // bintray 仓库名
    userOrg = 'xinpengfei520' // bintray 注册的用户名
    groupId = 'com.xsir' // compile 引用时的第 1 部分 groupId
    artifactId = 'PgyerAndroidAppUpdate' // compile 引用时的第 2 部分项目名
    publishVersion = versions.libReleaseName // compile引用时的第3部分版本号
    desc = 'update android app by pgyer app store.'
    website = 'https://github.com/xinpengfei520/pgyer-android-app-update'
}

/*
 * 发布到 bintray
 * ./gradlew clean build bintrayUpload -PbintrayUser=xinpengfei520 -PbintrayKey=BINTRAY_KEY -PdryRun=false
 */
